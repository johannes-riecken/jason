








****** Vocabulary/hcapdot ******
From J Wiki
< Vocabulary
Jump to: navigation, search
>> <<   Down_to:_Dyad   Back_to:_Vocabulary Thru_to:_Dictionary
      (m H. n) y x (m H. n) y Hypergeometric Conjunction
Vocabulary/rank000
=======================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================
x (m H. n) y sums x terms of a generalized_hypergeometric_series.
    * Operands m and n describe the series
    * Argument y gives the argument values(s) (may be real or complex).
Omitting x gives the limiting case as x tends to infinity.
   erf =: 3 : '(((2p_0.5)*y) % (^*:y)) * 1 H. 1.5 *: y'   NB. Error function
   erf 1
0.842701

   bessel1 =: dyad define    NB. Bessel function of the first kind Jx(y)
(((-: y)^x) % !x) *  '' H. (x+1) _0.25 * *: y
)
   1 bessel1 3
0.339059
=======================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================
**** Common Uses ****
Many common mathematical functions can be computed as instantiations of the generalized hypergeometric series by a choice of the operands m and n (atoms or lists).
These are classified into families, designated mFn according to the numbers of values in m and n. Each family has many members depending on the actual values of m and n.
The error function above is a member of 1F1 (the confluent hypergeometric functions of the first kind) and the Bessel function a member of 0F1 (the confluent hypergeometric limit functions).
The most important functions for physics are those of 2F1, which are called the hypergeometric functions.
Chapter 15 of Abramowitz_&_Stegun represent any given instance of the hypergeometric functions by F(a,b;c;z), where a, b and c are constants and z is a point in the object domain, the complex plane:
[Hyp.jpg]
The notation (a)n is the rising Pochhammer symbol, implemented in J as the stope_function  (a ^!.1 n)
To convert this F-notation to J syntax:  (m H. n) y
   m=: a,b
   n=: c
   y=: z
A convenient verb for this purpose is
F=: 3 : 0
NB. Convert F(a;b;c;z) into monadic H. call
'a b c'=. 3{.y
z=. > 3}.y
m=. a,b
n=. c
m H. n z
)
=======================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================
**** Examples ****
Abramowitz & Stegun, Chapter 15
[Asx.jpg]
Ancillary verbs for sample functions
ln=: ^.
arcsin=: _1&o.
arctan=: _3&o.
Sample points in the object domain (the disk of convergence 1>|z)
   ] z=: }. 5%~ i.5
0.2 0.4 0.6 0.8
Identities 15.1.3 to 15.1.6 with their equivalent functions
   F(1; 1; 2; z)                 NB. 15.1.3
1.11572 1.27706 1.52715 2.0118
   -(ln 1-z) % z
1.11572 1.27706 1.52715 2.0118

   F(1r2; 1; 3r2; z^2)           NB. 15.1.4
1.01366 1.05912 1.15525 1.37327
   -:(ln (1+z)%(1-z)) % z
1.01366 1.05912 1.15525 1.37327

   F(1r2; 1; 3r2; -z^2)          NB. 15.1.5
0.986978 0.951266 0.900699 0.843426
   (arctan z) % z
0.986978 0.951266 0.900699 0.843426

   F(1r2; 1r2; 3r2; z^2)         NB. 15.1.6
1.00679 1.02879 1.0725 1.15912
   (arcsin z) % z
1.00679 1.02879 1.0725 1.15912
=======================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================================

Retrieved from "https://code.jsoftware.com/mediawiki/index.php?title=Vocabulary/hcapdot&oldid=20800"
Category:
    * Voc
***** Navigation menu *****
**** Personal tools ****
    * Log_in
**** Namespaces ****
    * Page
    * Discussion
??
**** Variants ****
**** Views ****
    * Read
    * View_source
    * View_history
??
**** More ****
**** Search ****
[Unknown INPUT type][Search][Go]
**** Navigation ****
    * New_Users
    * Guides
    * NuVoc
    * System
    * Showcase
    * Library
    * Community
    * Recent_changes
    * Random_page
    * J_wiki_search
**** Tools ****
    * What_links_here
    * Related_changes
    * Special_pages
    * Printable_version
    * Permanent_link
    * Page_information
    * Cite_this_page
    * This page was last edited on 2 July 2017, at 21:59.
    * About_J_Wiki
    * [Powered_by_MediaWiki]
